    MODULE BASE_CLASS
    USE NODE_CLASS
    IMPLICIT NONE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    TYPE,PUBLIC :: BASE
        !->+++++++++++++++++++++++++++++++++++++++++++++
        INTEGER :: ID
        INTEGER :: SMD,NAD,NBD

        TYPE(NODE) :: NOD(2)
        INTEGER :: IINDEX(2)
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT), ALLOCATABLE:: QS(:),QD1T(:),QD2T(:)
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT),ALLOCATABLE :: TPC2D(:),TPC1D(:),TPC(:),TPQ1D(:),TPQ(:)

        REAL(RDT),ALLOCATABLE :: FCPPFCPP(:,:),FCPFCP(:,:),FCFC(:,:),FCPPFCP(:,:),FCPPFC(:,:),FCPFC(:,:),&
            FQPFQP(:,:),FQFQ(:,:),FQPFQ(:,:),&
            FCPPFQP(:,:),FCPPFQ(:,:),FCPFQP(:,:),FCPFQ(:,:),FCFQP(:,:),FCFQ(:,:)
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT) SV,SW,SPH,SU,SALP,SGE,SGZ
        REAL(RDT) SV1X,SW1X,SPH1X,SU1X,SALP1X,SGE1X,SGZ1X
        REAL(RDT) SV2X,SW2X

        REAL(RDT) SV1T,SW1T,SPH1T,SU1T,SALP1T,SGE1T,SGZ1T
        REAL(RDT) SV2T,SW2T,SPH2T,SU2T,SALP2T,SGE2T,SGZ2T

        REAL(RDT) SV1X1T, SW1X1T
        REAL(RDT) SV1X2T, SW1X2T
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT) :: EAT,EAEAT,EAZAT,EIZZT,EIEZT,EIEET,&
            EAC0T,EAC1T,EAC2T,EAC3T,&
            GEAT,GZAT,GEEAT,GEZAT,GZZAT,&
            GEAEBT,GEAZBT,GZAECT,GZAZCT,&
            GJT,GEJT,GZJT,&
            EAB0T,EAB1T,EAB2T,EAB3T,EAB4T,EAB5T,EAB6T,EAB7T,&
            EAB8T,EAB9T,EAB10T,EAB11T,EAB12T,EAB13T,EAB14T,EAB15T,EAB3PT,EAB8PT,&
            EAD0T,EAD1T,EAD2T,EAD3T,EAD4T,EAD5T,EAD6T,EAD7T,EAD0PT,EAD1PT,EAD2PT,&
            EAD3PT,EAD4PT,EAD6PT,EAD7PT

        REAL(RDT) :: EAEAB,EAZAB,GEAEBB,GEAZBB,GZAECB,GZAZCB,&
            EAB1B,EAB2B,EAB6B,EAB7B,EAC1B,EAC2B,&
            EAD1B,EAD2B,EAD1PB,EAD2PB,&
            EIEZB,EIEEB,EIZZB,EIZEB
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT) ::MSLT,MEMT,MZMT,IMEET,IMZZT,IMEZT,&
            MD0T,MD1T,MD2T,MD3T

        REAL(RDT) :: MEMB,MZMB,IMEZB,IMEEB,IMZZB,IMZEB,&
            MD1B,MD2B
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT) ::BTAT,TA0T,BSCT
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT) ::GGT
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT) :: OMGE(3),OMGE1D(3),VBE(3),VBE1D(3),HEE(3)
        REAL(RDT) :: TV1(3),TV2(3),TV3(3),HOMGB(3)

        REAL(RDT) :: TBR(3,3),TEB(3,3)

        REAL(RDT) :: XX
        !->+++++++++++++++++++++++++++++++++++++++++++++
        REAL(RDT) CTFTMP1,CTFTMP2
        !->+++++++++++++++++++++++++++++++++++++++++++++
        INTEGER :: IS_TIP
        REAL(RDT) :: LDT(3)
        real(rdt),ALLOCATABLE :: LDLM(:,:),LDKM(:,:),LDCM(:,:),LDMM(:,:)
        real(rdt),ALLOCATABLE :: LDLKM(:,:),LDLCM(:,:)
        !->+++++++++++++++++++++++++++++++++++++++++++++
        INTEGER,ALLOCATABLE :: MHIV2(:)
        INTEGER :: SMD_USED
        !->+++++++++++++++++++++++++++++++++++++++++++++
        INTEGER :: IS_AERO
        !->+++++++++++++++++++++++++++++++++++++++++++++
    CONTAINS
    PROCEDURE,PUBLIC :: CONSTRUCT_BASE
    PROCEDURE,PUBLIC :: UPDATE_ELE
    PROCEDURE,PUBLIC :: GETLV
    PROCEDURE,PUBLIC :: GETLVSS
    PROCEDURE,PUBLIC :: DECONSTRUCT_BASE

    PROCEDURE,PRIVATE :: GEOPRARAINTP
    PROCEDURE,PRIVATE :: MATPROPKINTP
    PROCEDURE,PRIVATE :: MATPROPMINTP
    PROCEDURE,PRIVATE :: SPF
    PROCEDURE,PRIVATE :: QSINTP
    PROCEDURE,PRIVATE :: UPLOCVARIBS
    PROCEDURE,PRIVATE :: TRANSMV

    END TYPE BASE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    CONTAINS
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !->+++++++++++++++PUBLIC++++++++++++++++++++++++++
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE CONSTRUCT_BASE(THIS,ID,INPU)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !初始化
    !->+++++++++++++++++++++++++++++++++++++++++++++
    USE INPUT_CLASS
    IMPLICIT NONE
    CLASS(BASE) :: THIS
    CLASS(INPUT) :: INPU
    INTEGER,INTENT(IN) :: ID

    INTEGER :: NAD,NBD,SMD
    INTEGER :: I,J

    NAD=4
    NBD=3
    SMD=23

    THIS%ID=ID
    THIS%NAD=NAD
    THIS%NBD=NBD
    THIS%SMD=SMD

    ALLOCATE(THIS%QS(THIS%SMD),THIS%QD1T(THIS%SMD),THIS%QD2T(THIS%SMD))

    ALLOCATE(THIS%TPC2D(NAD),THIS%TPC1D(NAD),THIS%TPC(NAD),THIS%TPQ1D(NBD),THIS%TPQ(NBD),&

        THIS%FCPPFCPP(NAD,NAD),THIS%FCPFCP(NAD,NAD),THIS%FCFC(NAD,NAD),THIS%FCPPFCP(NAD,NAD),&
        THIS%FCPPFC(NAD,NAD),THIS%FCPFC(NAD,NAD),&
        THIS%FQPFQP(NBD,NBD),THIS%FQFQ(NBD,NBD),THIS%FQPFQ(NBD,NBD),&
        THIS%FCPPFQP(NAD,NBD),THIS%FCPPFQ(NAD,NBD),THIS%FCPFQP(NAD,NBD),THIS%FCPFQ(NAD,NBD),&
        THIS%FCFQP(NAD,NBD),THIS%FCFQ(NAD,NBD))

    THIS%IINDEX(1:2)=(/ID,ID+1/)

    DO I=1,2
        J=THIS%IINDEX(I)
        CALL THIS%NOD(I)%CONSTRUCT_NODE(J,INPU%MAT%BTA(J),INPU%MAT%TAO(J),INPU%MAT%RL(J),&
            INPU%MAT%STIFF(:,:,J),INPU%MAT%MASS(:,:,J),INPU%MAT%CHO)
    END DO

    IF(THIS%IS_TIP.EQ.1) THEN
        ALLOCATE(THIS%LDLM(SMD,SMD),THIS%LDKM(SMD,SMD),THIS%LDCM(SMD,SMD),THIS%LDMM(SMD,SMD),&
            THIS%LDLKM(SMD,SMD),THIS%LDLCM(SMD,SMD))
    END IF

    THIS%QS=0.0D0
    THIS%QD1T=0.0D0
    THIS%QD2T=0.0D0
    
    IF(INPU%MAT%CHO.EQ.0) THEN
        THIS%SMD_USED=14
        ALLOCATE(THIS%MHIV2(THIS%SMD_USED))
        THIS%MHIV2 =(/3,4,7,8,11,14,10,13,1,2,5,6,9,12/)
    ELSE IF(INPU%MAT%CHO.EQ.1) THEN
        THIS%SMD_USED=23
        ALLOCATE(THIS%MHIV2(THIS%SMD_USED))
        THIS%MHIV2 =(/3,4,7,8,11,14,17,20,23,10,13,16,19,22,1,2,5,6,9,12,15,18,21/)
    END IF
    
    THIS%GGT=INPU%IPT%GGT
    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE DECONSTRUCT_BASE(THIS)
    IMPLICIT NONE
    CLASS(BASE) :: THIS

    DEALLOCATE(THIS%QS,THIS%QD1T,THIS%QD2T)

    DEALLOCATE(THIS%TPC2D,THIS%TPC1D,THIS%TPC,THIS%TPQ1D,THIS%TPQ,&

        THIS%FCPPFCPP,THIS%FCPFCP,THIS%FCFC,THIS%FCPPFCP,&
        THIS%FCPPFC,THIS%FCPFC,&
        THIS%FQPFQP,THIS%FQFQ,THIS%FQPFQ,&
        THIS%FCPPFQP,THIS%FCPPFQ,THIS%FCPFQP,THIS%FCPFQ,&
        THIS%FCFQP,THIS%FCFQ,&
        THIS%MHIV2)

    IF(THIS%IS_TIP.EQ.1) THEN
        DEALLOCATE(THIS%LDLM,THIS%LDKM,THIS%LDCM,THIS%LDMM,THIS%LDLKM,THIS%LDLCM)
    END IF

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE UPDATE_ELE(THIS,N,USP,US1TP,US2TP)
    IMPLICIT NONE
    CLASS(BASE) :: THIS
    INTEGER,INTENT(IN) :: N
    REAL(RDT),INTENT(IN) :: USP(N),US1TP(N),US2TP(N)

    THIS%QS=0.0D0
    THIS%QD1T=0.0D0
    THIS%QD2T=0.0D0

    THIS%QS(THIS%MHIV2)=USP
    THIS%QD1T(THIS%MHIV2)=US1TP
    THIS%QD2T(THIS%MHIV2)=US2TP

    IF(THIS%IS_TIP.EQ.1) THEN
        CALL THIS%TRANSMV()
        THIS%QD2T=MATMUL(THIS%LDLCM,THIS%QD2T)+MATMUL(THIS%LDMM,THIS%QD1T)
        THIS%QD1T=MATMUL(THIS%LDLCM,THIS%QD1T)
        THIS%QS=MATMUL(THIS%LDLKM,THIS%QS)
    END IF

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE GETLV(THIS,HE,SS,LL,BTAP,EL1,THP,THP1D,THP2D,OMG)
    IMPLICIT NONE
    CLASS(BASE) :: THIS
    REAL(RDT),INTENT(IN) :: THP,THP1D,THP2D,OMG,HE
    REAL(RDT),INTENT(IN)::SS,LL,BTAP,EL1

    CALL THIS%GETLVSS(SS,LL)
    CALL THIS%UPLOCVARIBS(HE,SS,LL,BTAP,EL1,THP,THP1D,THP2D,OMG)

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE GETLVSS(THIS,SS,LL)
    IMPLICIT NONE
    CLASS(BASE) :: THIS
    REAL(RDT),INTENT(IN)::SS,LL

    CALL THIS%GEOPRARAINTP(SS)
    CALL THIS%MATPROPKINTP(SS)
    CALL THIS%MATPROPMINTP(SS)
    CALL THIS%SPF(SS,LL)
    CALL THIS%QSINTP()

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !->+++++++++++++++PRIVATE++++++++++++++++++++++++++
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE GEOPRARAINTP(THIS,SS)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !几何参数插值
    !->+++++++++++++++++++++++++++++++++++++++++++++
    IMPLICIT NONE
    CLASS(BASE) :: THIS
    REAL(RDT),INTENT(IN) :: SS

    REAL(RDT),EXTERNAL::FWM

    THIS%BTAT=FWM(THIS%NOD(1)%BTA,THIS%NOD(2)%BTA,SS)
    THIS%TA0T=FWM(THIS%NOD(1)%TA0,THIS%NOD(2)%TA0,SS)
    THIS%BSCT=FWM(THIS%NOD(1)%BSC,THIS%NOD(2)%BSC,SS)

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE MATPROPKINTP(THIS,SS)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !截面刚度参数插值
    !->+++++++++++++++++++++++++++++++++++++++++++++
    IMPLICIT NONE
    CLASS(BASE) :: THIS
    REAL(RDT),INTENT(IN) :: SS

    REAL(RDT),EXTERNAL::FWM,FABSC

    THIS%EAT=FWM(THIS%NOD(1)%EA,THIS%NOD(2)%EA,SS)
    THIS%EAEAT=FWM(THIS%NOD(1)%EAETA,THIS%NOD(2)%EAETA,SS)
    THIS%EAZAT=FWM(THIS%NOD(1)%EAZTA,THIS%NOD(2)%EAZTA,SS)
    THIS%EIZZT=FWM(THIS%NOD(1)%EIZZ,THIS%NOD(2)%EIZZ,SS)
    THIS%EIEZT=FWM(THIS%NOD(1)%EIEZ,THIS%NOD(2)%EIEZ,SS)
    THIS%EIEET=FWM(THIS%NOD(1)%EIEE,THIS%NOD(2)%EIEE,SS)

    THIS%EAC0T=FWM(THIS%NOD(1)%EAC0,THIS%NOD(2)%EAC0,SS)
    THIS%EAC1T=FWM(THIS%NOD(1)%EAC1,THIS%NOD(2)%EAC1,SS)
    THIS%EAC2T=FWM(THIS%NOD(1)%EAC2,THIS%NOD(2)%EAC2,SS)
    THIS%EAC3T=FWM(THIS%NOD(1)%EAC3,THIS%NOD(2)%EAC3,SS)

    THIS%GEAT=FWM(THIS%NOD(1)%GEA,THIS%NOD(2)%GEA,SS)
    THIS%GZAT=FWM(THIS%NOD(1)%GZA,THIS%NOD(2)%GZA,SS)
    THIS%GEEAT=FWM(THIS%NOD(1)%GEEA,THIS%NOD(2)%GEEA,SS)
    THIS%GEZAT=FWM(THIS%NOD(1)%GEZA,THIS%NOD(2)%GEZA,SS)
    THIS%GZZAT=FWM(THIS%NOD(1)%GZZA,THIS%NOD(2)%GZZA,SS)

    THIS%GEAEBT=FWM(THIS%NOD(1)%GEAEB,THIS%NOD(2)%GEAEB,SS)
    THIS%GEAZBT=FWM(THIS%NOD(1)%GEAZB,THIS%NOD(2)%GEAZB,SS)
    THIS%GZAECT=FWM(THIS%NOD(1)%GZAEC,THIS%NOD(2)%GZAEC,SS)
    THIS%GZAZCT=FWM(THIS%NOD(1)%GZAZC,THIS%NOD(2)%GZAZC,SS)

    THIS%GJT=FWM(THIS%NOD(1)%GJ,THIS%NOD(2)%GJ,SS)
    THIS%GEJT=FWM(THIS%NOD(1)%GEJ,THIS%NOD(2)%GEJ,SS)
    THIS%GZJT=FWM(THIS%NOD(1)%GZJ,THIS%NOD(2)%GZJ,SS)

    THIS%EAB0T=FWM(THIS%NOD(1)%EAB0,THIS%NOD(2)%EAB0,SS)
    THIS%EAB1T=FWM(THIS%NOD(1)%EAB1,THIS%NOD(2)%EAB1,SS)
    THIS%EAB2T=FWM(THIS%NOD(1)%EAB2,THIS%NOD(2)%EAB2,SS)
    THIS%EAB3T=FWM(THIS%NOD(1)%EAB3,THIS%NOD(2)%EAB3,SS)
    THIS%EAB4T=FWM(THIS%NOD(1)%EAB4,THIS%NOD(2)%EAB4,SS)
    THIS%EAB5T=FWM(THIS%NOD(1)%EAB5,THIS%NOD(2)%EAB5,SS)
    THIS%EAB6T=FWM(THIS%NOD(1)%EAB6,THIS%NOD(2)%EAB6,SS)
    THIS%EAB7T=FWM(THIS%NOD(1)%EAB7,THIS%NOD(2)%EAB7,SS)
    THIS%EAB8T=FWM(THIS%NOD(1)%EAB8,THIS%NOD(2)%EAB8,SS)
    THIS%EAB9T=FWM(THIS%NOD(1)%EAB9,THIS%NOD(2)%EAB9,SS)
    THIS%EAB10T=FWM(THIS%NOD(1)%EAB10,THIS%NOD(2)%EAB10,SS)
    THIS%EAB11T=FWM(THIS%NOD(1)%EAB11,THIS%NOD(2)%EAB11,SS)
    THIS%EAB12T=FWM(THIS%NOD(1)%EAB12,THIS%NOD(2)%EAB12,SS)
    THIS%EAB13T=FWM(THIS%NOD(1)%EAB13,THIS%NOD(2)%EAB13,SS)
    THIS%EAB14T=FWM(THIS%NOD(1)%EAB14,THIS%NOD(2)%EAB14,SS)
    THIS%EAB15T=FWM(THIS%NOD(1)%EAB15,THIS%NOD(2)%EAB15,SS)
    THIS%EAB3PT=FWM(THIS%NOD(1)%EAB3P,THIS%NOD(2)%EAB3P,SS)
    THIS%EAB8PT=FWM(THIS%NOD(1)%EAB8P,THIS%NOD(2)%EAB8P,SS)

    THIS%EAD0T=FWM(THIS%NOD(1)%EAD0,THIS%NOD(2)%EAD0,SS)
    THIS%EAD1T=FWM(THIS%NOD(1)%EAD1,THIS%NOD(2)%EAD1,SS)
    THIS%EAD2T=FWM(THIS%NOD(1)%EAD2,THIS%NOD(2)%EAD2,SS)
    THIS%EAD3T=FWM(THIS%NOD(1)%EAD3,THIS%NOD(2)%EAD3,SS)
    THIS%EAD4T=FWM(THIS%NOD(1)%EAD4,THIS%NOD(2)%EAD4,SS)
    THIS%EAD5T=FWM(THIS%NOD(1)%EAD5,THIS%NOD(2)%EAD5,SS)
    THIS%EAD6T=FWM(THIS%NOD(1)%EAD6,THIS%NOD(2)%EAD6,SS)
    THIS%EAD7T=FWM(THIS%NOD(1)%EAD7,THIS%NOD(2)%EAD7,SS)
    THIS%EAD0PT=FWM(THIS%NOD(1)%EAD0P,THIS%NOD(2)%EAD0P,SS)
    THIS%EAD1PT=FWM(THIS%NOD(1)%EAD1P,THIS%NOD(2)%EAD1P,SS)
    THIS%EAD2PT=FWM(THIS%NOD(1)%EAD2P,THIS%NOD(2)%EAD2P,SS)
    THIS%EAD3PT=FWM(THIS%NOD(1)%EAD3P,THIS%NOD(2)%EAD3P,SS)
    THIS%EAD4PT=FWM(THIS%NOD(1)%EAD4P,THIS%NOD(2)%EAD4P,SS)
    THIS%EAD6PT=FWM(THIS%NOD(1)%EAD6P,THIS%NOD(2)%EAD6P,SS)
    THIS%EAD7PT=FWM(THIS%NOD(1)%EAD7P,THIS%NOD(2)%EAD7P,SS)

    THIS%EAEAB=FABSC(THIS%EAEAT,-THIS%EAZAT,THIS%BTAT)
    THIS%EAZAB=FABSC(THIS%EAZAT,THIS%EAEAT,THIS%BTAT)

    THIS%GEAEBB=FABSC(THIS%GEAEBT,-THIS%GEAZBT,THIS%BTAT)
    THIS%GEAZBB=FABSC(THIS%GEAZBT,THIS%GEAEBT,THIS%BTAT)
    THIS%GZAECB=FABSC(THIS%GZAECT,-THIS%GZAZCT,THIS%BTAT)
    THIS%GZAZCB=FABSC(THIS%GZAZCT,THIS%GZAECT,THIS%BTAT)

    THIS%EAB1B=FABSC(THIS%EAB1T,-THIS%EAB2T,THIS%BTAT)
    THIS%EAB2B=FABSC(THIS%EAB2T,THIS%EAB1T,THIS%BTAT)
    THIS%EAB6B=FABSC(THIS%EAB6T,-THIS%EAB7T,THIS%BTAT)
    THIS%EAB7B=FABSC(THIS%EAB7T,THIS%EAB6T,THIS%BTAT)

    THIS%EAC1B=FABSC(THIS%EAC1T,-THIS%EAC2T,THIS%BTAT)
    THIS%EAC2B=FABSC(THIS%EAC2T,THIS%EAC1T,THIS%BTAT)

    THIS%EAD1B=FABSC(THIS%EAD1T,-THIS%EAD2T,THIS%BTAT)
    THIS%EAD2B=FABSC(THIS%EAD2T,THIS%EAD1T,THIS%BTAT)
    THIS%EAD1PB=FABSC(THIS%EAD1PT,-THIS%EAD2PT,THIS%BTAT)
    THIS%EAD2PB=FABSC(THIS%EAD2PT,THIS%EAD1PT,THIS%BTAT)

    THIS%EIEZB=FABSC(THIS%EIEZT,-THIS%EIEET,THIS%BTAT)
    THIS%EIEEB=FABSC(THIS%EIEET,THIS%EIEZT,THIS%BTAT)
    THIS%EIZZB=FABSC(THIS%EIZZT,-THIS%EIEZT,THIS%BTAT)
    THIS%EIZEB=FABSC(THIS%EIEZT,THIS%EIZZT,THIS%BTAT)

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE MATPROPMINTP(THIS,SS)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !截面质量参数插值
    !->+++++++++++++++++++++++++++++++++++++++++++++
    IMPLICIT NONE
    CLASS(BASE) :: THIS
    REAL(RDT),INTENT(IN) :: SS

    REAL(RDT),EXTERNAL::FWM,FABSC

    THIS%MSLT=FWM(THIS%NOD(1)%MSL,THIS%NOD(2)%MSL,SS)

    THIS%MEMT=FWM(THIS%NOD(1)%MEM,THIS%NOD(2)%MEM,SS)
    THIS%MZMT=FWM(THIS%NOD(1)%MZM,THIS%NOD(2)%MZM,SS)

    THIS%IMEET=FWM(THIS%NOD(1)%IMEE,THIS%NOD(2)%IMEE,SS)
    THIS%IMZZT=FWM(THIS%NOD(1)%IMZZ,THIS%NOD(2)%IMZZ,SS)
    THIS%IMEZT=FWM(THIS%NOD(1)%IMEZ,THIS%NOD(2)%IMEZ,SS)

    THIS%MD0T=FWM(THIS%NOD(1)%MD0,THIS%NOD(2)%MD0,SS)
    THIS%MD1T=FWM(THIS%NOD(1)%MD1,THIS%NOD(2)%MD1,SS)
    THIS%MD2T=FWM(THIS%NOD(1)%MD2,THIS%NOD(2)%MD2,SS)
    THIS%MD3T=FWM(THIS%NOD(1)%MD3,THIS%NOD(2)%MD3,SS)

    THIS%MEMB=FABSC(THIS%MEMT,-THIS%MZMT,THIS%BTAT)
    THIS%MZMB=FABSC(THIS%MZMT,THIS%MEMT,THIS%BTAT)

    THIS%IMEZB=FABSC(THIS%IMEZT,-THIS%IMEET,THIS%BTAT)
    THIS%IMEEB=FABSC(THIS%IMEET,THIS%IMEZT,THIS%BTAT)
    THIS%IMZZB=FABSC(THIS%IMZZT,-THIS%IMEZT,THIS%BTAT)
    THIS%IMZEB=FABSC(THIS%IMEZT,THIS%IMZZT,THIS%BTAT)

    THIS%MD1B=FABSC(THIS%MD1T,-THIS%MD2T,THIS%BTAT)
    THIS%MD2B=FABSC(THIS%MD2T,THIS%MD1T,THIS%BTAT)

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE UPLOCVARIBS(THIS,HE,SS,LL,BTAP,EL1,THP,THP1D,THP2D,OMG)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !坐标系转换矩阵等变量
    !->+++++++++++++++++++++++++++++++++++++++++++++
    IMPLICIT NONE
    CLASS(BASE) :: THIS
    REAL(RDT),INTENT(IN) :: THP,THP1D,THP2D,OMG,HE
    REAL(RDT),INTENT(IN)::SS,LL,BTAP,EL1

    REAL(RDT),EXTERNAL:: FABSC,triseries

    INTEGER I,J
    INTEGER IP,IS

    REAL(RDT) OMGV(3)
    REAL(RDT) OMGB(3),OMGB1D(3)
    REAL(RDT) R0(3)
    REAL(RDT) VBR(3),VBB(3),VBB1D(3)
    REAL(RDT) HEB(3)

    REAL(RDT) TMPR(3,3), TMBP(3,3)
    REAL(RDT) TMBP1T(3,3)
    REAL(RDT) TMS(3,3),TMA(3,3),TMT(3,3)

    REAL(RDT) LDST,LDAT,LDTT

    LDST=0.0D0
    LDAT=0.0D0
    LDTT=0.0D0

    IF(THIS%IS_TIP.EQ.1) THEN
        LDST=-THIS%LDT(3)
        LDAT=-THIS%LDT(2)
    ELSE
        LDST=0.0D0
        LDAT=0.0D0
    END IF

    THIS%XX=SS*LL

    CALL TXYZ(TMPR,2,-BTAP)
    CALL TXYZ(TMBP,1,THP)
    CALL MAMU(THIS%TBR,3,3,3,TMBP,TMPR)

    CALL TXYZD1T(TMBP1T,1,THP,THP1D)

    CALL TXYZ(TMS,3,-LDST)
    CALL TXYZ(TMA,2,-LDAT)
    CALL TXYZ(TMT,1,LDTT)
    CALL MATMLYTP(THIS%TEB, 3, 3, 3, 3, TMT, TMA, TMS)

    OMGV=OMG*(/0.0, 0.0, 1.0/)
    CALL MVMU2(OMGB,3,3,THIS%TBR,OMGV)
    OMGB(1)=OMGB(1)+THP1D
    CALL MVMU2(THIS%OMGE,3,3,THIS%TEB,OMGB)

    CALL MVMU2(OMGB1D,3,3,TMPR,OMGV)
    CALL MVMU2(OMGB1D,3,3,TMBP1T,OMGB1D)
    OMGB1D(1)=OMGB1D(1)+THP2D
    CALL MVMU2(THIS%OMGE1D,3,3,THIS%TEB,OMGB1D)

    R0=EL1*(/1.0, 0.0, 0.0/)
    CALL VCP(VBR,OMGV,R0)
    CALL MVMU2(VBB,3,3,THIS%TBR,VBR)
    CALL MVMU2(THIS%VBE,3,3,THIS%TEB,VBB)

    CALL MVMU2(VBB1D,3,3,TMPR,VBR)
    CALL MVMU2(VBB1D,3,3,TMBP1T,VBB1D)
    CALL MVMU2(THIS%VBE1D,3,3,THIS%TEB,VBB1D)

    HEB=HE*(/1.0, 0.0, 0.0/)
    CALL MVMU2(THIS%HEE,3,3,THIS%TEB,HEB)

    DO 5 I=1,3
        IP=MOD(I,3)+1
        IS=MOD(I+1,3)+1
        THIS%TV1(I)=THIS%OMGE(IP)**2+THIS%OMGE(IS)**2
        THIS%TV2(I)=THIS%OMGE(I)*THIS%OMGE(IP)-THIS%OMGE1D(IS)
        THIS%TV3(I)=THIS%OMGE(I)*THIS%OMGE(IS)+THIS%OMGE1D(IP)
        THIS%HOMGB(I)=THIS%TV1(I)*THIS%HEE(I)-THIS%TV2(I)*THIS%HEE(IP)-THIS%TV3(I)*THIS%HEE(IS)&
            -THIS%OMGE(IP)*THIS%VBE(IS)+THIS%OMGE(IS)*THIS%VBE(IP)-THIS%VBE1D(I)
5   CONTINUE

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE QSINTP(THIS)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !变形量插值
    !->+++++++++++++++++++++++++++++++++++++++++++++
    IMPLICIT NONE
    CLASS(BASE) :: THIS

    REAL(RDT),EXTERNAL :: VIP
    INTEGER :: NAD,NBD

    REAL(RDT) QSV(THIS%NAD),QSW(THIS%NAD),QSPH(THIS%NBD),QSU(THIS%NBD)
    REAL(RDT) QSALP(THIS%NBD),QSGE(THIS%NBD),QSGZ(THIS%NBD)

    REAL(RDT) QV1T(THIS%NAD),QW1T(THIS%NAD),QPH1T(THIS%NBD),QU1T(THIS%NBD)
    REAL(RDT) QALP1T(THIS%NBD),QGE1T(THIS%NBD),QGZ1T(THIS%NBD)

    REAL(RDT) QV2T(THIS%NAD),QW2T(THIS%NAD),QU2T(THIS%NBD)
    REAL(RDT) QALP2T(THIS%NBD),QGE2T(THIS%NBD),QGZ2T(THIS%NBD)

    NAD=THIS%NAD
    NBD=THIS%NBD

    QSV(:)=THIS%QS(1:NAD)
    QSW(:)=THIS%QS(NAD+1:2*NAD)
    QSPH(:)=THIS%QS(2*NAD+1:2*NAD+NBD)
    QSU(:)=THIS%QS(2*NAD+NBD+1:2*NAD+2*NBD)
    QSALP(:)=THIS%QS(2*NAD+2*NBD+1:2*NAD+3*NBD)
    QSGE(:)=THIS%QS(2*NAD+3*NBD+1:2*NAD+4*NBD)
    QSGZ(:)=THIS%QS(2*NAD+4*NBD+1:2*NAD+5*NBD)

    QV1T(:)=THIS%QD1T(1:NAD)
    QW1T(:)=THIS%QD1T(NAD+1:2*NAD)
    QPH1T(:)=THIS%QD1T(2*NAD+1:2*NAD+NBD)
    QU1T(:)=THIS%QD1T(2*NAD+NBD+1:2*NAD+2*NBD)
    QALP1T(:)=THIS%QD2T(2*NAD+2*NBD+1:2*NAD+3*NBD)
    QGE1T(:)=THIS%QD1T(2*NAD+3*NBD+1:2*NAD+4*NBD)
    QGZ1T(:)=THIS%QD1T(2*NAD+4*NBD+1:2*NAD+5*NBD)

    QV2T(:)=THIS%QD2T(1:NAD)
    QW2T(:)=THIS%QD2T(NAD+1:2*NAD)
    QU2T(:)=THIS%QD2T(2*NAD+NBD+1:2*NAD+2*NBD)
    QALP2T(:)=THIS%QD2T(2*NAD+2*NBD+1:2*NAD+3*NBD)
    QGE2T(:)=THIS%QD2T(2*NAD+3*NBD+1:2*NAD+4*NBD)
    QGZ2T(:)=THIS%QD2T(2*NAD+4*NBD+1:2*NAD+5*NBD)


    THIS%SV=VIP(NAD,THIS%TPC,QSV)
    THIS%SW=VIP(NAD,THIS%TPC,QSW)
    THIS%SPH=VIP(NBD,THIS%TPQ,QSPH)
    THIS%SU=VIP(NBD,THIS%TPQ,QSU)
    THIS%SALP=VIP(NBD,THIS%TPQ,QSALP)
    THIS%SGE=VIP(NBD,THIS%TPQ,QSGE)
    THIS%SGZ=VIP(NBD,THIS%TPQ,QSGZ)

    THIS%SV1X=VIP(NAD,THIS%TPC1D,QSV)
    THIS%SW1X=VIP(NAD,THIS%TPC1D,QSW)
    THIS%SPH1X=VIP(NBD,THIS%TPQ1D,QSPH)
    THIS%SU1X=VIP(NBD,THIS%TPQ1D,QSU)
    THIS%SALP1X=VIP(NBD,THIS%TPQ1D,QSALP)
    THIS%SGE1X=VIP(NBD,THIS%TPQ1D,QSGE)
    THIS%SGZ1X=VIP(NBD,THIS%TPQ1D,QSGZ)

    THIS%SV2X=VIP(NAD,THIS%TPC2D,QSV)
    THIS%SW2X=VIP(NAD,THIS%TPC2D,QSW)

    THIS%SV1T=VIP(NAD,THIS%TPC,QV1T)
    THIS%SW1T=VIP(NAD,THIS%TPC,QW1T)
    THIS%SPH1T=VIP(NBD,THIS%TPQ,QPH1T)
    THIS%SU1T=VIP(NBD,THIS%TPQ,QU1T)
    THIS%SALP1T=VIP(NBD,THIS%TPQ,QALP1T)
    THIS%SGE1T=VIP(NBD,THIS%TPQ,QGE1T)
    THIS%SGZ1T=VIP(NBD,THIS%TPQ,QGZ1T)

    THIS%SV1X2T=VIP(NAD,THIS%TPC1D,QV2T)
    THIS%SW1X2T=VIP(NAD,THIS%TPC1D,QW2T)

    THIS%SV1X1T=VIP(NAD,THIS%TPC1D,QV1T)
    THIS%SW1X1T=VIP(NAD,THIS%TPC1D,QW1T)

    THIS%SV2T=VIP(NAD,THIS%TPC,QV2T)
    THIS%SW2T=VIP(NAD,THIS%TPC,QW2T)
    THIS%SU2T=VIP(NBD,THIS%TPQ,QU2T)
    THIS%SALP2T=VIP(NBD,THIS%TPQ,QALP2T)
    THIS%SGE2T=VIP(NBD,THIS%TPQ,QGE2T)
    THIS%SGZ2T=VIP(NBD,THIS%TPQ,QGZ2T)

    END SUBROUTINE
    !->+++++++++++++++++++++++++++++++++++++++++++++
    SUBROUTINE SPF(THIS,SS,LL)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !型函数插值
    !->+++++++++++++++++++++++++++++++++++++++++++++
    IMPLICIT NONE
    CLASS(BASE) THIS

    REAL(RDT),INTENT(IN) :: SS,LL

    INTEGER :: NAD,NBD
    INTEGER :: NAOOD,NBOOD

    NAD=THIS%NAD
    NBD=THIS%NBD

    NAOOD=2
    CALL UU(THIS%TPC2D,NAOOD,NAD,SS,LL)
    NAOOD=1
    CALL UU(THIS%TPC1D,NAOOD,NAD,SS,LL)
    NAOOD=0
    CALL UU(THIS%TPC,NAOOD,NAD,SS,LL)

    NBOOD=1
    CALL VV(THIS%TPQ1D,NBOOD,NBD,SS,LL)
    NBOOD=0
    CALL VV(THIS%TPQ,NBOOD,NBD,SS,LL)


    CALL VOP(THIS%FCPPFCPP,NAD,NAD,THIS%TPC2D,THIS%TPC2D)
    CALL VOP(THIS%FCPFCP,NAD,NAD,THIS%TPC1D,THIS%TPC1D)
    CALL VOP(THIS%FCFC,NAD,NAD,THIS%TPC,THIS%TPC)
    CALL VOP(THIS%FCPPFCP,NAD,NAD,THIS%TPC2D,THIS%TPC1D)
    CALL VOP(THIS%FCPPFC,NAD,NAD,THIS%TPC2D,THIS%TPC)
    CALL VOP(THIS%FCPFC,NAD,NAD,THIS%TPC1D,THIS%TPC)

    CALL VOP(THIS%FQPFQP,NBD,NBD,THIS%TPQ1D,THIS%TPQ1D)
    CALL VOP(THIS%FQFQ,NBD,NBD,THIS%TPQ,THIS%TPQ)
    CALL VOP(THIS%FQPFQ,NBD,NBD,THIS%TPQ1D,THIS%TPQ)

    CALL VOP(THIS%FCPPFQP,NAD,NBD,THIS%TPC2D,THIS%TPQ1D)
    CALL VOP(THIS%FCPPFQ,NAD,NBD,THIS%TPC2D,THIS%TPQ)

    CALL VOP(THIS%FCPFQP,NAD,NBD,THIS%TPC1D,THIS%TPQ1D)
    CALL VOP(THIS%FCPFQ,NAD,NBD,THIS%TPC1D,THIS%TPQ)

    CALL VOP(THIS%FCFQP,NAD,NBD,THIS%TPC,THIS%TPQ1D)
    CALL VOP(THIS%FCFQ,NAD,NBD,THIS%TPC,THIS%TPQ)

    END SUBROUTINE

    SUBROUTINE TRANSMV(THIS)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    !TRANSFORMATION FOR THE VECTOR OF NODAL DEGREES OF FREEDOM(Page 398)
    !The transformation for the vector of nodal degrees of freedom are 23 by 23
    !matrices where a typical element at the i-th row and j-th column of a transformation matrix [A] is denoted by A(i, j)
    !->+++++++++++++++++++++++++++++++++++++++++++++
    IMPLICIT NONE
    CLASS(BASE) THIS

    real(rdt) :: LDST,LDAT,SS,SA,CS,CA
    real(rdt) :: SV1X,SW1X,SPH,SPHPB
    real(rdt) :: SV1X1T,SW1X1T,SPH1T

    LDST=-THIS%LDT(3)
    LDAT=-THIS%LDT(2)

    SV1X=THIS%QS(6)
    SW1X=THIS%QS(2)
    SPH=THIS%QS(9)
    SPHPB=SPH+THIS%NOD(1)%BTA

    SV1X1T=THIS%QD1T(6)
    SW1X1T=THIS%QD1T(2)
    SPH1T=THIS%QD1T(9)

    SS=SIN(LDST)
    SA=SIN(LDAT)

    CS=COS(LDST)
    CA=COS(LDAT)


    !*********************线性转换矩阵************************
    THIS%LDLM=0.0

    THIS%LDLM(1,1)=CS
    THIS%LDLM(1,5)=SS*SA
    THIS%LDLM(1,12)=SS*CA

    THIS%LDLM(2,2)=CA
    THIS%LDLM(2,9)=-SA

    THIS%LDLM(3,3)=CS
    THIS%LDLM(3,7)=SS*SA
    THIS%LDLM(3,14)=SS*CA

    THIS%LDLM(4,4)=CA
    THIS%LDLM(4,11)=-SA

    THIS%LDLM(5,5)=CA
    THIS%LDLM(5,12)=-SA

    THIS%LDLM(6,2)=-SS*SA
    THIS%LDLM(6,6)=CS
    THIS%LDLM(6,9)=-SS*CA

    THIS%LDLM(7,7)=CA
    THIS%LDLM(7,14)=-SA

    THIS%LDLM(8,4)=-SS*SA
    THIS%LDLM(8,8)=CS
    THIS%LDLM(8,11)=-SS*CA

    THIS%LDLM(9,2)=CS*SA
    THIS%LDLM(9,6)=SS
    THIS%LDLM(9,9)=CS*CA

    THIS%LDLM(10,2)=0.5*CS*SA
    THIS%LDLM(10,4)=0.5*CS*SA
    THIS%LDLM(10,6)=0.5*SS
    THIS%LDLM(10,8)=0.5*SS
    THIS%LDLM(10,10)=CS*CA

    THIS%LDLM(11,4)=CS*SA
    THIS%LDLM(11,8)=SS
    THIS%LDLM(11,11)=CS*CA

    THIS%LDLM(12,1)=-SS
    THIS%LDLM(12,5)=CS*SA
    THIS%LDLM(12,12)=CS*CA

    THIS%LDLM(13,1)=-0.5*SS
    THIS%LDLM(13,3)=-0.5*SS
    THIS%LDLM(13,5)=0.5*CS*SA
    THIS%LDLM(13,7)=0.5*CS*SA
    THIS%LDLM(13,13)=CS*CA

    THIS%LDLM(14,3)=-SS
    THIS%LDLM(14,7)=CS*SA
    THIS%LDLM(14,14)=CS*CA

    THIS%LDLM(15,15)=CS*CA

    THIS%LDLM(16,16)=CS*CA

    THIS%LDLM(17,17)=CS*CA

    THIS%LDLM(18,18)=CA

    THIS%LDLM(19,19)=CA

    THIS%LDLM(20,20)=CA

    THIS%LDLM(21,18)=-SS*SA

    THIS%LDLM(21,21)=CA
    !LDLM(21,21)=CS

    THIS%LDLM(22,19)=-SS*SA
    THIS%LDLM(22,22)=CA
    !LDLM(22,22)=CS

    THIS%LDLM(23,20)=-SS*SA
    THIS%LDLM(23,23)=CA
    !LDLM(23,23)=CS

    !*********************非线性刚度转换矩阵************************

    THIS%LDKM=0.0D0

    THIS%LDKM(2,2)=CS*SS*CA*SA*SPHPB-CS**2*SA*SW1X
    THIS%LDKM(2,9)=SS**2*CA*SW1X

    THIS%LDKM(6,6)=-SS*SA*SPHPB
    THIS%LDKM(6,9)=-CS*SA**2*SV1X

    THIS%LDKM(9,9)=CS*SA*SW1X-SS*SA**2*SV1X

    !*********************非线性阻尼转换矩阵************************

    THIS%LDCM=0.0D0

    THIS%LDCM(2,2)=CS*SS*CA*SA*SPHPB-CS**2*SA*SW1X
    THIS%LDCM(2,6)=SS**2*CA*SPHPB-CS**2*SA*SV1X
    THIS%LDCM(2,9)=SS**2*CA*SW1X+CS*SS*CA*SA*SV1X

    THIS%LDCM(6,2)=-CS*SA**2*SPHPB
    THIS%LDCM(6,6)=-SS*SA*SPHPB
    THIS%LDCM(6,9)=-CS*SA**2*SV1X-SS*SA*SW1X

    THIS%LDCM(9,2)=-SS*SA**2*SPHPB
    THIS%LDCM(9,6)=CS*SA*SPHPB
    THIS%LDCM(9,9)=CS*SA*SW1X-SS*SA**2*SV1X

    !*********************非线性质量转换矩阵************************

    THIS%LDMM=0.0D0

    THIS%LDMM(2,2)=CS*SS*CA*SA*SPH1T-CS**2*SA*SW1X1T
    THIS%LDMM(2,6)=SS**2*CA*SPH1T-CS**2*SA*SV1X1T
    THIS%LDMM(2,9)=SS**2*CA*SW1X1T+CS*SS*CA*SA*SV1X1T

    THIS%LDMM(6,2)=-CS*SA**2*SPH1T
    THIS%LDMM(6,6)=-SS*SA*SPH1T
    THIS%LDMM(6,9)=-CS*SA**2*SV1X1T-SS*SA*SW1X1T

    THIS%LDMM(9,2)=-SS*SA**2*SPH1T
    THIS%LDMM(9,6)=CS*SA*SPH1T
    THIS%LDMM(9,9)=CS*SA*SW1X1T-SS*SA**2*SV1X1T

    THIS%LDLKM=THIS%LDLM+THIS%LDKM
    THIS%LDLCM=THIS%LDLM+THIS%LDCM

    END SUBROUTINE

    END MODULE
